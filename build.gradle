import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "java"
}

// naming
version = '2.1.1-SNAPSHOT'
group = 'dev.spaceseries'
def packageGroup = group + '.spacechat'

repositories {
    mavenCentral()
    jcenter()

    maven { url 'https://repo.yakovliam.com/releases/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    maven { url 'https://oss.sonatype.org/content/repositories/central/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '16.0.1'

    // spigot
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'

    // space api
    compileOnly 'dev.spaceseries:spaceapi:1.0.12'

    // placeholderAPI
    compileOnly 'me.clip:placeholderapi:2.10.9'

    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    compileOnly group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'

    // https://mvnrepository.com/artifact/org.ow2.asm/asm
    compileOnly group: 'org.ow2.asm', name: 'asm', version: '9.1'

    // https://mvnrepository.com/artifact/org.ow2.asm/asm-commons
    compileOnly group: 'org.ow2.asm', name: 'asm-commons', version: '9.1'

    // bstats
    implementation "org.bstats:bstats-bukkit:2.2.1"

    // caffeine
    compileOnly 'com.github.ben-manes.caffeine:caffeine:2.9.1'

    // adventure
    compileOnly "net.kyori:adventure-api:4.8.1"
    compileOnly "net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT"
    compileOnly "net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"
}

artifacts {
    archives shadowJar
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    archiveName("SpaceChat-${project.version}.jar")

    relocate 'org.bstats', "${packageGroup}.lib.bstats"
    relocate 'dev.spaceseries.spaceapi', "${packageGroup}.lib.spaceapi"
    relocate 'net.kyori', "${packageGroup}.lib.kyori"
}

processResources {
    filter ReplaceTokens, tokens: [version: project.version]
}